plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdk 36
    namespace 'com.yuyan'
    defaultConfig {
        applicationId "com.yuyan.pinyin"
        minSdk 23
        targetSdk 36
        versionCode Integer.valueOf(versionCodeDate())
        versionName versionNameDate()

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        ndk {
            abiFilters 'arm64-v8a'
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }

        lintOptions {
            abortOnError false
        }
    }
    
    packagingOptions {
        jniLibs {
            useLegacyPackaging true
        }
    }

    signingConfigs {
        release {

            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storeFile file('baidu_pass_is_android.keystore')
            storePassword 'android'

        }
    }

    //多渠道打包
    flavorDimensions = ["default"]
    productFlavors {
        online {
            dimension "default"
            applicationIdSuffix  ".online"
        }
        offline {
            dimension "default"
            applicationIdSuffix  ".offline"
        }
    }

    buildTypes {
        release {
            // 混淆
            minifyEnabled true
            shrinkResources true
            applicationIdSuffix  ".release"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            signingConfig signingConfigs.release
        }

        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.release
        }

    }


    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17) // 改为 17 或 21
        }
    }

    //    第三方库使用java1.8
    compileOptions {
        sourceCompatibility 17
        targetCompatibility 17
    }

    // 统一 Kotlin 的 JVM 目标版本（KSP 会继承此配置）
    kotlinOptions {
        jvmTarget = "17" // 必须与 compileOptions 中的版本一致
    }
    android.applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            outputFileName = "yuyanIme_${versionCodeDate()}_${variant.getProductFlavors()[0].name}_${variant.buildType.name}.apk"   // offline      online
        }
    }
}

static def versionCodeDate() {
    return new Date().format("yyyyMMddHH", TimeZone.getTimeZone("GMT+8"))
}

static def versionNameDate() {
    return new Date().format("yyyyMMdd.HH", TimeZone.getTimeZone("GMT+8"))
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':yuyansdk')
}
